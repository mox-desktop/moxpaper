{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.services.moxpaper;

  toLua =
    value:
    let
      recurse = v: toLua v;
      generators = {
        bool = b: if b then "true" else "false";
        int = toString;
        float = toString;
        string = s: ''"${lib.escape [ ''"'' ] s}"'';
        path = p: ''"${p}"'';
        null = "nil";
        list = vs: "{\n  ${lib.concatMapStringsSep ",\n  " recurse vs}\n}";
        attrs = vs: ''
          {
            ${lib.concatStringsSep ",\n" (
              lib.mapAttrsToList (k: v: "[${generators.string k}] = ${recurse v}") vs
            )}
          }'';
      };
    in
    if builtins.isAttrs value then
      generators.attrs value
    else if builtins.isList value then
      generators.list value
    else
      generators.${builtins.typeOf value} value;
in
{
  options.services.moxpaper = {
    enable = lib.mkEnableOption "moxpaper";
    package = lib.mkPackageOption pkgs "moxpaper" { };
    settings = lib.mkOption {
      type =
        with lib.types;
        let
          valueType = nullOr (oneOf [
            bool
            int
            float
            str
            path
            (attrsOf valueType)
            (listOf valueType)
          ]);
        in
        valueType;
      default = { };
      description = ''
        moxpaper configuration in Nix format that will be converted to Lua.
      '';
    };
    extraConfig = lib.mkOption {
      type = lib.types.lines;
      default = "";
      example = lib.literalExpression ''
        -- Your lua code / config here
        local mylib = require 'mylib';
        return {
          usemylib = mylib.do_fun();
          font = wezterm.font("JetBrains Mono"),
          font_size = 16.0,
          color_scheme = "Tomorrow Night",
          hide_tab_bar_if_only_one_tab = true,
          default_prog = { "zsh", "--login", "-c", "tmux attach -t dev || tmux new -s dev" },
          keys = {
            {key="n", mods="SHIFT|CTRL", action="ToggleFullScreen"},
          }
        }
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    home.packages = [ cfg.package ];
    xdg.configFile."mox/moxpaper/config.lua" = lib.mkIf (cfg.extraConfig != "" || cfg.settings != { }) {
      text = ''
        -- Generated by Home Manager
        ${cfg.extraConfig}
        ${lib.optionalString (cfg.settings != { }) "\nreturn ${toLua cfg.settings}"}
      '';
    };

    systemd.user.services.moxpaper = {
      Install = {
        WantedBy = [ config.wayland.systemd.target ];
      };

      Unit = {
        Description = "Mox desktop environment wallpaper daemon";
        PartOf = [ config.wayland.systemd.target ];
        After = [ config.wayland.systemd.target ];
        ConditionEnvironment = "WAYLAND_DISPLAY";
        X-Restart-Triggers = [ config.xdg.configFile."mox/moxpaper/config.lua".source ];
      };

      Service = {
        ExecStart = "${lib.getExe cfg.package}";
        Restart = "always";
        RestartSec = "10";
      };
    };
  };
}
